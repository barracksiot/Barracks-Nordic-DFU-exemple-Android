apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'


repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        options.bottom("""
            <script type="text/javascript">
                (function(w,d,s,l,i){
                    w[l]=w[l]||[];
                    w[l].push({"gtm.start":new Date().getTime(),event:"gtm.js"});
                    var f=d.getElementsByTagName(s)[0],j=d.createElement(s),dl=l!="dataLayer"?"&l="+l:"";
                    j.async=true;
                    j.src="http://www.googletagmanager.com/gtm.js?id="+i+dl;
                    f.parentNode.insertBefore(j,f);
                 })(window,document,"script","dataLayer","GTM-PBN7TZ");
            </script>
        """.replaceAll(/\r|\n/, ""))
        destinationDir reporting.file("javadoc/${variant.name}")
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.robolectric:shadows-support-v4:3.1'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
}
